buildscript {
    repositories {
        google()
        jcenter {
            content {
                // https://youtrack.jetbrains.com/issue/IDEA-261387
                includeModule("org.jetbrains.trove4j", "trove4j")
            }
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

allprojects {
    group = 'org.mapsforge'
    version = 'master-SNAPSHOT'
    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

    ext.androidBuildVersionTools = "30.0.3"
    ext.jUnitVersion = "4.12"

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
            logging.captureStandardError LogLevel.INFO
            logging.captureStandardOutput LogLevel.INFO
        }
    }
}

static def androidCompileSdk() { return 30 }

// 14 for Support Library, 16 for sqlite-android
static def androidMinSdk() { return 16 }

static def androidTargetSdk() { return 30 }

static def versionCode() { return 1 }

def versionName() { return version }

subprojects {
    repositories {
        google()
        jcenter {
            content {
                // https://youtrack.jetbrains.com/issue/IDEA-261387
                includeModule("org.jetbrains.trove4j", "trove4j")
            }
        }
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
        options.encoding = 'UTF-8'
        if (JavaVersion.current().isJava9Compatible()) {
            if (!project.properties.containsKey('android')) {
                if (project.name.contains('writer'))
                    options.compilerArgs.addAll(['--release', '8'])
                else
                    options.compilerArgs.addAll(['--release', '7'])
            }
        }
    }
}
