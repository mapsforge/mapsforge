apply plugin: 'com.android.application'

dependencies {
    implementation project(":mapsforge-map-android")
    implementation project(":mapsforge-themes")
    implementation project(":mapsforge-poi-android")
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.core:core:1.6.0'
    implementation 'androidx.fragment:fragment:1.3.6'
    androidTestImplementation 'com.jayway.android.robotium:robotium-solo:5.6.3'
}

android {
    compileSdk androidCompileSdk()

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        versionCode project.versionCode()
        versionName project.versionName()
        minSdk androidMinSdk()
        targetSdk androidTargetSdk()
        testApplicationId "org.mapsforge.samples.android.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            file("${rootDir}/sqlite-android/natives").eachDir() { dir ->
                jniLibs.srcDirs += "${dir.path}/lib"
            }
        }
        androidTest {
            java.srcDirs = ['src/androidTest']
        }
    }

    lintOptions {
        abortOnError false
    }
}
