apply plugin: 'java-library'
apply plugin: 'maven-publish'

dependencies {
    implementation project(":mapsforge-poi")
    implementation 'com.google.guava:guava:33.0.0-jre'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.2'
    implementation 'org.locationtech.jts:jts-core:1.20.0'
    implementation 'org.xerial:sqlite-jdbc:3.43.0.0'
    compileOnly 'org.openstreetmap.osmosis:osmosis-core:0.47'
}

jar {
    from('src/main/config/') {
        filter {
            line -> line.replace('${poi.writer.version}', version)
        }
    }
}

tasks.register('fatJar', Jar) {
    dependsOn configurations.runtimeClasspath
    archiveClassifier = 'jar-with-dependencies'
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact fatJar
        }
    }
}

if (project.hasProperty("SONATYPE_USERNAME")) {
    afterEvaluate {
        project.apply from: "${rootProject.projectDir}/deploy.gradle"
    }
}
