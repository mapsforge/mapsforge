apply plugin: 'java-library'
apply plugin: 'maven-publish'

dependencies {
    compileOnly files("${System.env.ANDROID_HOME}/platforms/android-${androidCompileSdk()}/android.jar")
}

file('natives').eachDir() { dir ->
    task("nativesJar-${dir.name}", type: Jar) {
        classifier "natives-${dir.name}"
        from(dir.path)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            file('natives').eachDir() { dir ->
                artifact tasks["nativesJar-${dir.name}"]
            }
        }
    }
}

if (project.hasProperty("SONATYPE_USERNAME")) {
    afterEvaluate {
        project.apply from: "${rootProject.projectDir}/deploy.gradle"
    }
}

if (System.getenv('ANDROID_HOME') == null) {
    throw new GradleException("Environment variable ANDROID_HOME needs to be set to SDK folder")
}
