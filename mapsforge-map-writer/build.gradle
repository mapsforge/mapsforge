apply plugin: 'java-library'
apply plugin: 'maven'

dependencies {
    implementation project(":mapsforge-core")
    implementation 'com.google.guava:guava:23.0'
    implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation 'org.locationtech.jts:jts-core:1.15.0'
    compileOnly 'org.openstreetmap.osmosis:osmosis-core:0.46'
    testImplementation "junit:junit:$jUnitVersion"
}

sourceSets.test.compileClasspath += configurations.compileOnly
sourceSets.test.runtimeClasspath += configurations.compileOnly

jar {
    from('src/main/config/') {
        filter {
            line -> line.replace('${mapfile.writer.version}', version)
        }
    }
}

task fatJar(type: Jar, dependsOn: classes) {
    classifier = 'jar-with-dependencies'
    from {
        (configurations.compileClasspath - configurations.compileOnly).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

artifacts {
    archives fatJar
}

if (project.hasProperty("SONATYPE_USERNAME")) {
    afterEvaluate {
        project.apply from: "${rootProject.projectDir}/deploy.gradle"
    }
}
